plugins {
  id 'fabric-loom' version '1.2-SNAPSHOT' apply false
  id 'org.barfuin.gradle.taskinfo' version '2.1.0'
}

apply plugin: 'base'

var outputDir = project.layout.projectDirectory.dir("build/libs")

subprojects {
  apply plugin: 'fabric-loom'

  version = project.mod_version
  group = project.maven_group

  loom {
    runConfigs.configureEach {
      ideConfigGenerated = true
    }
  }

  tasks.withType(GenerateModuleMetadata).configureEach {
    it.enabled = false
  }

  tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
    it.options.encoding = "UTF-8"
  }

  java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
  }

  jar {
    from("LICENSE") {
      rename { "${it}_$project.archivesBaseName" }
    }
  }

  remapJar {
    destinationDirectory = outputDir
  }

  dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
  }

  processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
      expand "version": project.version
    }
  }
}

clean.doFirst {
  delete "${rootDir}/build/"
}
